plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'maven'
    id 'application'
	id 'com.github.jk1.tcdeps' version '0.19'
    id 'com.jaredsburrows.license' version '0.8.42'
}

group = 'org.javacs'
version = '0.1.13'

mainClassName = 'org.javacs.kt.MainKt'
description = 'Code completions, diagnostics and more for Kotlin'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def debugPort = 8000
def debugArgs = '-agentlib:jdwp=transport=dt_socket,server=y,address=8000,suspend=n,quiet=y'

startScripts {
    applicationName = "kotlin-language-server"
}

repositories {
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    maven { url uri("$projectDir/lib") }
    mavenCentral()
    teamcityServer {
        url = teamCityUrl
        credentials {
            username = teamCityUsername
            password = teamCityPassword
        }
    }
}

configurations {
    kotlinJVMLib
}

dependencies {
    def kotlinPluginTC = "$kotlinBuildType:$kotlinBuild:kotlin-plugin-${kotlinPluginBuild}.zip!/Kotlin"
    
    implementation project(':shared')
    implementation 'com.google.guava:guava:21.0'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.5.0'
    implementation 'org.jetbrains.kotlin:kotlin-compiler'
    implementation 'org.jetbrains.kotlin:kotlin-scripting-compiler'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains:fernflower:1.0'
    implementation 'com.pinterest.ktlint:ktlint-core:0.34.2'
    implementation 'com.pinterest.ktlint:ktlint-ruleset-standard:0.34.2'
    implementation fileTree(dir: "$projectDir/lib-kotlin", include: ["*.jar"])
    
    // TODO: Isolate J2K dependency from the plugin:
    kotlinJVMLib tc("$kotlinPluginTC/lib/kotlin-plugin.jar")
    
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'junit:junit:4.11'
    testImplementation 'org.openjdk.jmh:jmh-core:1.20'
    testImplementation 'org.jetbrains.kotlin:kotlin-script-runtime'
    
    annotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.20'
}

configurations.all { config ->
    config.resolutionStrategy {
        preferProjectModules()
    }
}

applicationDistribution.into('bin') {
    fileMode = 0755
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task copyKotlinJVMLib(type: Sync) {
    from configurations.kotlinJVMLib
    into file('lib-kotlin')
}

task fixFilePermissions(type: Exec) {
    // When running on macOS or Linux the start script
    // needs executable permissions to run.
    onlyIf { !System.getProperty("os.name").toLowerCase().contains("windows") }
    commandLine 'chmod', '+x', "${installDist.destinationDir}/bin/kotlin-language-server"
}

task debugRun(type: JavaExec) {
    main = mainClassName
    classpath sourceSets.main.runtimeClasspath
    standardInput = System.in
    
    jvmArgs debugArgs
    doLast {
        println "Using debug port $debugPort"
    }
}

task debugStartScripts(type: CreateStartScripts) {
    applicationName = startScripts.applicationName
    mainClassName = startScripts.mainClassName
    outputDir = installDist.destinationDir.toPath().resolve('bin').toFile()
    classpath = startScripts.classpath
    defaultJvmOpts = [debugArgs]
}

task installDebugDist(type: Sync) {
    dependsOn installDist
    finalizedBy debugStartScripts
}

run {
    standardInput = System.in
}

test {
    testLogging {
        events "failed"
        exceptionFormat "short"
    }
}

compileKotlin.dependsOn copyKotlinJVMLib
installDist.finalizedBy fixFilePermissions
build.finalizedBy installDist
