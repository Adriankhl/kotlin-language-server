plugins {
    id 'com.github.node-gradle.node' version '2.0.0'
}

/** Prepares the grammars for packaging. */
task copyGrammars(type: Sync) {
    from "$rootDir/grammars/textmate/Kotlin.tmLanguage.json"
    into file('grammars')
}

/** Prepares the language server binaries for packaging. */
task copyLanguageServer(type: Sync) {
    dependsOn ':server:installDist'
    from project(':server').tasks.installDist.destinationDir
    into file('install')
}

/** Prepares resources used by the extension. */
task prepare { dependsOn copyGrammars, copyLanguageServer }

npmInstall { dependsOn prepare }

task install { dependsOn npmInstall }

task build { dependsOn install }

/**
 * Links the extension into your local Atom
 * package directory.
 */
task link(type: Exec) {
    dependsOn install
    workingDir projectDir
    commandLine 'apm', 'link'
}
