plugins {
    id 'com.github.node-gradle.node' version '2.0.0'
}

/** Prepares the TextMate grammar for packaging. */
task copyTextMateGrammar(type: Copy) {
    from "$rootDir/grammars/textmate/Kotlin.tmLanguage.json"
    into file('grammars')
}

/** Prepares the Tree-Sitter grammar for packaging. */
task copyTreeSitterGrammar(type: Copy) {
    from "$rootDir/grammars/tree-sitter/tree-sitter-kotlin.cson"
    into file('grammars')
}

/** Prepares the language server binaries for packaging. */
task copyLanguageServer(type: Sync) {
    dependsOn ':server:installDist'
    from project(':server').tasks.installDist.destinationDir
    into file('install')
}

/** Prepares resources used by the extension. */
task prepare { dependsOn copyTextMateGrammar, copyTreeSitterGrammar, copyLanguageServer }

npmInstall { dependsOn prepare }

task install { dependsOn npmInstall }

task build { dependsOn install }

/**
 * Rebuilds the extension's native modules (the
 * Tree-Sitter parser) using Atom's node version.
 */
task apmRebuild(type: Exec) {
	dependsOn install
	workingDir projectDir
	commandLine 'apm', 'rebuild'
}

/**
 * Links the extension into your local Atom
 * package directory.
 */
task apmLink(type: Exec) {
    dependsOn apmRebuild
    workingDir projectDir
    commandLine 'apm', 'link'
}
