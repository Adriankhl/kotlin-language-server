plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://cache-redirector.jetbrains.com/kotlin.bintray.com/kotlin-plugin' }
    }
}

configure(subprojects.findAll { it.name != 'grammars' }) {
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    publishing {
        repositories {
            maven {
                name = 'GitHubPackages'
                url = uri('https://maven.pkg.github.com/fwcd/kotlin-language-server')
                credentials {
                    username = project.findProperty('gpr.user') ?: System.getenv('GPR_USERNAME')
                    password = project.findProperty('gpr.key') ?: System.getenv('GPR_PASSWORD')
                }
            }
        }

        publications {
            gpr(MavenPublication) {
                from components.java
            }
        }
    }
}
